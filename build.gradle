plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.0'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.owasp.dependencycheck' version '12.1.1'
    id "org.sonarqube" version "6.2.0.5505"
    id 'jacoco'
}

group = 'ru.tpu.hostel'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

def repoUser = project.findProperty("privateRepoUsername") ?: System.getenv("INTERNAL_REPO_LOGIN")
def repoPass = project.findProperty("privateRepoPassword") ?: System.getenv("INTERNAL_REPO_PASSWORD")

repositories {
    mavenCentral()
    google()
    gradlePluginPortal()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven {
        url = "http://82.202.138.26:8081/repository/internal/"
        allowInsecureProtocol = true
        credentials {
            username = repoUser
            password = repoPass
        }
    }
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:2025.0.0"
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "TpaBKa251_Schedules" // Замените на ключ вашего проекта в SonarCloud
        property "sonar.organization", "hostel-service" // Замените на имя вашей организации
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.token", System.getenv("SONAR_TOKEN") // Используется токен из секретов GitHub
        property "sonar.coverage.jacoco.xmlReportPaths", layout.buildDirectory.file("reports/jacoco/test/jacocoTestReport.xml")
        property "sonar.coverage.exclusions", "**/*Generated*.java,**/model/*.java" // Исключения из анализа
        property "sonar.java.binaries", "build/classes/java/main" // Путь к скомпилированным классам
        property "sonar.exclusions", "**/TimeSlotSchedulesConfig.java"
    }
}

ext {
    set('internalLibraryVersion', "1.3.4")
}

dependencies {
    // Внутрення библиотека
    implementation "ru.tpu.hostel:hostel-core:${internalLibraryVersion}"
    implementation "ru.tpu.hostel:hostel-feign:${internalLibraryVersion}"
    implementation "ru.tpu.hostel:hostel-amqp:${internalLibraryVersion}"
    implementation "ru.tpu.hostel:notification:${internalLibraryVersion}"

    // Spring Boot starters
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-amqp'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-cache'

    // Spring Cloud
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign:4.3.0'

    // БД
    runtimeOnly 'org.postgresql:postgresql:42.7.3'
    implementation 'org.flywaydb:flyway-core:11.9.0'
    implementation 'org.flywaydb:flyway-database-postgresql:11.9.0'
    implementation 'org.ehcache:ehcache:3.10.8:jakarta'
    implementation 'org.hibernate:hibernate-jcache:6.6.15.Final'

    // Доки
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.8'

    // Утилиты
    implementation 'org.mapstruct:mapstruct:1.6.3'
    implementation 'commons-io:commons-io:2.19.0'
    implementation 'org.apache.commons:commons-compress:1.27.1'
    implementation 'org.apache.commons:commons-collections4:4.5.0'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.19.0'
    implementation 'com.squareup:javapoet:1.13.0'

    // Безопасность
    implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.6'
    implementation 'org.springframework.security:spring-security-crypto:6.5.0'

    // Наблюдение (трассировка, метрики, экспорт)
    implementation 'io.micrometer:micrometer-tracing-bridge-otel:1.5.0'
    implementation 'io.opentelemetry:opentelemetry-exporter-otlp:1.50.0'
    implementation 'io.micrometer:micrometer-registry-prometheus:1.15.0'
    implementation 'com.github.loki4j:loki-logback-appender:1.5.2'
    implementation 'net.logstash.logback:logstash-logback-encoder:8.1'

    // Cache
    implementation 'com.github.ben-manes.caffeine:caffeine:3.2.0'

    // Jakarta
    implementation 'jakarta.validation:jakarta.validation-api:3.1.1'

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.38'
    annotationProcessor 'org.projectlombok:lombok:1.18.38'
    annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'

    // MapStruct
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    // Тесты
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.testcontainers:junit-jupiter:1.19.8'
    testImplementation 'org.testcontainers:postgresql:1.19.8'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    testImplementation 'org.mockito:mockito-core:5.12.0'
    testImplementation 'org.assertj:assertj-core:3.26.0'
    testImplementation 'org.instancio:instancio-core:4.6.0'
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy jacocoTestReport // Генерируем отчет после тестов
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required.set(true)
        html.required.set(false)
        csv.required.set(false)
    }
}

jacoco {
    toolVersion = "0.8.12" // Последняя версия
}

jar {
    manifest {
        attributes 'Main-Class': 'ru.tpu.hostel.schedules.SchedulesApplication'
    }
}
