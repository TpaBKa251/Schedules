plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.5'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.owasp.dependencycheck' version '8.3.1'
    id "org.sonarqube" version "6.0.1.5171"
    id 'jacoco'
}

def env = new Properties()
file("repository-config.env").withInputStream { env.load(it) }

group = 'ru.tpu.hostel'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()

    maven {
        url "http://82.202.138.26:8081/repository/internal/"
        allowInsecureProtocol = true
        credentials {
            username = env['username']
            password = env['password']
        }
    }

}

sonarqube {
    properties {
        property "sonar.projectKey", "TpaBKa251_Schedules" // Замените на ключ вашего проекта в SonarCloud
        property "sonar.organization", "hostel-service" // Замените на имя вашей организации
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.login", System.getenv("SONAR_TOKEN") // Используется токен из секретов GitHub
        property "sonar.coverage.jacoco.xmlReportPaths", layout.buildDirectory.file("reports/jacoco/test/jacocoTestReport.xml")
        property "sonar.coverage.exclusions", "**/*Generated*.java,**/model/*.java" // Исключения из анализа
        property "sonar.java.binaries", "build/classes/java/main" // Путь к скомпилированным классам
        property "sonar.exclusions", "**/TimeSlotSchedulesConfig.java"
    }
}

dependencies {

    implementation 'ru.tpu.hostel:hostel-core:1.0.2'
    implementation 'ru.tpu.hostel:hostel-feign:1.0.2'


    implementation platform("org.springframework.cloud:spring-cloud-dependencies:2023.0.2")
    //implementation 'ch.qos.logback:logback-core:1.5.13'
    implementation 'commons-io:commons-io:2.14.0'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.3.0'
    runtimeOnly 'org.postgresql:postgresql'
    implementation "org.flywaydb:flyway-database-postgresql"
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    implementation 'org.springframework.boot:spring-boot-starter-amqp'
    implementation 'org.springframework.boot:spring-boot-starter-actuator' // Для актюаторов

    implementation 'org.apache.commons:commons-compress:1.26.0'
    implementation 'org.apache.poi:poi:5.2.3' // Для работы с .xls файлами
    implementation 'org.apache.poi:poi-ooxml:5.2.3' // Для работы с .xlsx файлами
    implementation 'org.apache.commons:commons-collections4:4.4' // Необходимая зависимость
    implementation 'org.apache.xmlbeans:xmlbeans:5.1.1' // Для работы с .xlsx (XML-структуры)

    implementation 'jakarta.validation:jakarta.validation-api:3.0.2'

    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    implementation 'io.jsonwebtoken:jjwt-impl:0.11.5'
    implementation 'io.jsonwebtoken:jjwt-jackson:0.11.5'

    compileOnly 'org.projectlombok:lombok'

    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
    implementation 'org.mapstruct:mapstruct:1.5.3.Final'
    annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.3.Final'


    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:postgresql'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'
    testImplementation 'org.mockito:mockito-core:5.2.0'
    testImplementation 'org.assertj:assertj-core:3.24.2'
    testImplementation 'org.instancio:instancio-core:2.13.0'
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy jacocoTestReport // Генерируем отчет после тестов
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required.set(true) // Включаем XML для SonarCloud
        html.required.set(false)
        csv.required.set(false)
    }
}

jacoco {
    toolVersion = "0.8.11" // Последняя версия
}

jar {
    manifest {
        attributes 'Main-Class': 'ru.tpu.hostel.schedules.SchedulesApplication'
    }
}
